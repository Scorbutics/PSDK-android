
plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.google.devtools.ksp' version "1.9.10-1.0.13"
}

android {
    compileSdkVersion 34

    sourceSets {
        main {
            assets.srcDirs += ['src/main/ruby']
            jniLibs.srcDir('jniLibs')
        }
    }

    packagingOptions.jniLibs.useLegacyPackaging true

    defaultConfig {
        applicationId "com.psdk.starter"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        externalNativeBuild {
            cmake {
                cFlags "-fdeclspec"
                cppFlags "-std=c++17"
                arguments "-DANDROID_STL=c++_shared","-DANDROID_PLATFORM=android-26","-DANDROID_ABI=arm64-v8a","-DCMAKE_BUILD_TYPE=Debug"
            }
        }
        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            externalNativeBuild {
                cmake {
                    cFlags "-fdeclspec"
                    cppFlags "-std=c++17"
                    arguments "-DANDROID_STL=c++_shared","-DANDROID_PLATFORM=android-26","-DANDROID_ABI=arm64-v8a","-DCMAKE_BUILD_TYPE=Release"
                }
            }
        }
        debug {
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters 'arm64-v8a'
                debugSymbolLevel 'FULL'
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    ndkVersion '25.0.8775105'
    namespace 'com.psdk'
}

kotlin {
    jvmToolchain(JavaVersion.VERSION_17.majorVersion.toInteger())
}

dependencies {
    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    implementation fileTree(dir: 'libs', include: '*.jar')
    implementation(libs.apkzlib)
    implementation(libs.apksig)
    implementation(libs.zipalign)
    implementation(libs.arsc)
    implementation(libs.bcpkix.jdk15on)
    implementation(libs.appcompat)
    implementation(libs.material)
    implementation(libs.constraintlayout)
    implementation(libs.zip4j)
    implementation(libs.legacy)
    implementation(libs.kotlinx.serialization)
    implementation libs.kotlin.stdlib
}
