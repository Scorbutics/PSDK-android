
plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 33

    sourceSets {
        main {
            assets.srcDirs += ['src/main/ruby']
        }
    }

    packagingOptions.jniLibs.useLegacyPackaging true

    defaultConfig {
        applicationId "com.psdk.starter"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        externalNativeBuild {
            cmake {
                cFlags "-fdeclspec"
                cppFlags "-std=c++17"
                arguments "-DANDROID_STL=c++_shared","-DANDROID_PLATFORM=android-26","-DANDROID_ABI=arm64-v8a","-DCMAKE_BUILD_TYPE=Debug"
            }
        }
        ndk {
            abiFilters 'arm64-v8a'
            debugSymbolLevel 'FULL'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
    ndkVersion '25.0.8775105'
    namespace 'com.psdk'
}

task appInternalDistribution(type: Zip) {
    archiveFileName = "app-internal.zip"
    destinationDirectory = file("./src/main/assets")
    var input = "./src/main/ext-native-libs/lib"

    inputs.dir(input)
    outputs.file("$destinationDirectory/$archiveFileName")

    from input
}

preBuild.dependsOn appInternalDistribution

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    implementation(libs.apkzlib)
    implementation(libs.apksig)
    implementation(libs.bcpkix.jdk15on)
    implementation(libs.appcompat)
    implementation(libs.material)
    implementation(libs.constraintlayout)
    implementation(libs.zip4j)
    implementation(libs.legacy)
    implementation libs.kotlin.stdlib
}
